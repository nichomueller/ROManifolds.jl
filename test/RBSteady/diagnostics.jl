module DiagnosticTests

using DrWatson
using Gridap
using Gridap.MultiField
using GridapROMs
using GridapROMs.RBSteady
using GridapROMs.RBTransient
using GridapSolvers
using Serialization
using Test
using Plots

function main(
  method=:pod,compression=:global,hypred_strategy=:deim;
  tol=1e-4,nparams=15,nparams_res=floor(Int,nparams/3),
  nparams_jac=floor(Int,nparams/4),sketch=:sprn,ncentroids=2
  )

  method = method ∈ (:pod,:ttsvd) ? method : :pod
  compression = compression ∈ (:global,:local) ? compression : :global
  hypred_strategy = hypred_strategy ∈ (:none,:affine,:deim,:sopt,:rbf) ? hypred_strategy : :deim

  println("Running test with $compression ($method, $hypred_strategy) strategy")

  pdomain = (1,10,-1,5,1,2)
  pspace = ParamSpace(pdomain)

  domain = (0,1,0,1)
  partition = (8,8)
  model = method==:ttsvd ? TProductDiscreteModel(domain,partition) : CartesianDiscreteModel(domain,partition)

  order = 2
  degree = 2*order

  Ω = Triangulation(model)
  dΩ = Measure(Ω,degree)

  a(μ) = x -> μ[1]*exp(-x[1])
  aμ(μ) = parameterise(a,μ)

  g(μ) = x -> VectorValue(-(μ[2]*x[2]+μ[3])*x[2]*(1.0-x[2]),0.0)*(x[1]==0.0)
  gμ(μ) = parameterise(g,μ)

  stiffness(μ,(u,p),(v,q),dΩ) = ∫(aμ(μ)*∇(v)⊙∇(u))dΩ - ∫(p*(∇⋅(v)))dΩ + ∫(q*(∇⋅(u)))dΩ
  res(μ,(u,p),(v,q),dΩ) = stiffness(μ,(u,p),(v,q),dΩ)

  trian_res = (Ω,)
  trian_stiffness = (Ω,)
  domains = FEDomains(trian_res,trian_stiffness)

  reffe_u = ReferenceFE(lagrangian,VectorValue{2,Float64},order)
  reffe_p = ReferenceFE(lagrangian,Float64,order-1)
  test_u = TestFESpace(Ω,reffe_u;conformity=:H1,dirichlet_tags=[1,2,3,4,5,6,7])
  test_p = TestFESpace(Ω,reffe_p;conformity=:H1)
  trial_u = ParamTrialFESpace(test_u,gμ)
  trial_p = ParamTrialFESpace(test_p)
  test = MultiFieldFESpace([test_u,test_p];style=BlockMultiFieldStyle())
  trial = MultiFieldFESpace([trial_u,trial_p];style=BlockMultiFieldStyle())

  energy = BlockNorm((H1(),L2()))
  coupling = DivCoupling()

  if method == :pod
    state_reduction = SupremizerReduction(coupling,tol,energy;nparams,sketch,compression,ncentroids)
  elseif method == :ttsvd
    state_reduction = SupremizerReduction(coupling,fill(tol,3),energy;nparams,sketch,compression,ncentroids)
  end

  fesolver = LUSolver()
  rbsolver = RBSolver(fesolver,state_reduction;nparams_res,nparams_jac,hypred_strategy)

  feop = LinearParamOperator(res,stiffness,pspace,trial,test,domains)
  
  dir = datadir("diagnostics")
  isdir(dir) && rm(dir;recursive=true)
  create_dir(dir)

  tols = [1e-1,1e-3,1e-5]
  run_test(dir,rbsolver,feop,tols)

  dgn = rom_diagnostics(dir,rbsolver,feop)
  println(dgn)
end

for method in (:pod,:ttsvd), compression in (:local,:global), hypred_strategy in (:none,:affine,:deim,:sopt,:rbf)
  main(method,compression,hypred_strategy)
end

end
