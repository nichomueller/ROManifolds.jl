function ParamDataStructures.get_all_data(a::PVector)
  vector_partition = map(a.vector_partition) do values
    get_all_data(values)
  end
  GenericPArray(vector_partition,a.index_partition)
end

function ParamDataStructures.get_all_data(a::PSparseMatrix)
  matrix_partition = map(a.matrix_partition) do values
    get_all_data(values)
  end
  GenericPArray(matrix_partition,flat_row_partition(a))
end

"""
    struct GenericPArray{V,A,B,C,D,T,N} <: AbstractArray{T,N}
      array_partition::A
      index_partition::B
      unpartitioned_axes::C
      cache::D
    end

Same as [`PVector`](@ref), but while the latter always stores a vector with entries
partitioned on different cores, this structure stores an array (not necessarily
a vector) partitioned along the first dimension (row-wise)
"""
struct GenericPArray{V,A,B,C,D,T,N} <: AbstractArray{T,N}
  array_partition::A
  index_partition::B
  unpartitioned_axes::C
  cache::D
  @doc """
      GenericPArray(array_partition,index_partition)

  Create an instance of [`GenericPArray`](@ref) from the underlying properties
  `array_partition` and `index_partition`.
  """
  function GenericPArray(
    array_partition::AbstractArray{<:AbstractArray{T,N}},
    index_partition,
    unpartitioned_axes=axes(getany(array_partition))[2:end],
    cache=PartitionedArrays.p_vector_cache(array_partition,index_partition)
    ) where {T,N}

    @notimplementedif N > 2 "For now only generic partitioned vectors/matrices are supported."
    V = eltype(array_partition)
    A = typeof(array_partition)
    B = typeof(index_partition)
    C = typeof(unpartitioned_axes)
    D = typeof(cache)
    new{V,A,B,C,D,T,N}(array_partition,index_partition,unpartitioned_axes,cache)
  end
end

const GenericPVector{V,A,B,C,D,T} = GenericPArray{V,A,B,C,D,T,1}
const GenericPMatrix{V,A,B,C,D,T} = GenericPArray{V,A,B,C,D,T,2}

PartitionedArrays.partition(a::GenericPArray) = a.array_partition
Base.axes(a::GenericPArray) = (PRange(a.index_partition),a.unpartitioned_axes...)
Base.size(a::GenericPArray) = length.(axes(a))

function PartitionedArrays.local_values(a::GenericPArray)
  partition(a)
end

function PartitionedArrays.own_values(a::GenericPArray)
  map(own_values,partition(a),partition(axes(a,1)))
end

function PartitionedArrays.ghost_values(a::GenericPArray)
  map(ghost_values,partition(a),partition(axes(a,1)))
end

function GridapDistributed.local_views(a::GenericPArray)
  partition(a)
end

function Base.getindex(a::GenericPArray,gid::Int)
  PartitionedArrays.scalar_indexing_action(a)
end

function Base.setindex!(a::GenericPArray,v,gid::Int)
  PartitionedArrays.scalar_indexing_action(a)
end

function Base.show(io::IO,k::MIME"text/plain",a::GenericPArray)
  T = eltype(partition(a))
  s = size(a)
  np = length(partition(a))
  map_main(partition(a)) do values
    println(io,"GenericPArray{$T} of size $s partitioned into $np parts")
  end
end

function PartitionedArrays.assemble!(a::GenericPArray)
  assemble!(+,a)
end

function PartitionedArrays.assemble!(o,a::GenericPArray)
  t = assemble!(o,partition(a),a.cache)
  @async begin
    wait(t)
    map(ghost_values(a)) do a
      fill!(a,zero(eltype(a)))
    end
    a
  end
end

function PartitionedArrays.consistent!(a::GenericPArray)
  insert(a,b) = b
  cache = map(reverse,a.cache)
  t = assemble!(insert,partition(a),cache)
  @async begin
    wait(t)
    a
  end
end

function Base.similar(a::GenericPArray,::Type{T},inds::Tuple) where T
  rows,uaxes... = inds
  @check isa(rows,PRange)
  values = map(partition(a),partition(rows)) do values,indices
    inds = (local_length(indices),map(length,uaxes)...)
    similar(values,T,inds)
  end
  GenericPArray(values,partition(rows),uaxes)
end

function Base.similar(::Type{<:GenericPArray{V}},inds::Tuple) where V
  rows,uaxes... = inds
  @check isa(rows,PRange)
  values = map(partition(rows)) do indices
    inds = (local_length(indices),map(length,uaxes)...)
    similar(values,T,inds)
  end
  GenericPArray(values,partition(rows),uaxes)
end

function GenericPArray(::UndefInitializer,index_partition,uaxes...)
  GenericPArray{Vector{Float64}}(undef,index_partition,uaxes...)
end

function GenericPArray{A}(::UndefInitializer,index_partition,uaxes...) where A
  array_partition = map(index_partition) do indices
    inds = (local_length(indices),map(length,uaxes)...)
    similar(A,inds)
  end
  GenericPArray(array_partition,index_partition)
end

function Base.copy!(a::GenericPArray,b::GenericPArray)
  @assert size(a) == size(b)
  copyto!(a,b)
end

function Base.copyto!(a::GenericPArray,b::GenericPArray)
  if partition(axes(a,1)) === partition(axes(b,1))
    map(copy!,partition(a),partition(b))
  elseif PartitionedArrays.matching_own_indices(axes(a,1),axes(b,1))
    map(copy!,own_values(a),own_values(b))
  else
    error("Trying to copy a GenericPArray into another one with a different data layout. This case is not implemented yet. It would require communications.")
  end
  a
end

function Base.fill!(a::GenericPArray,v)
  map(partition(a)) do values
    fill!(values,v)
  end
  a
end

function Base.:(==)(a::GenericPArray,b::GenericPArray)
  @boundscheck @assert PartitionedArrays.matching_own_indices(axes(a,1),axes(b,1))
  length(a) == length(b) &&
  reduce(&,map(==,own_values(a),own_values(b)),init=true)
end

function Base.any(f::Function,a::GenericPArray)
  partials = map(own_values(a)) do o
    any(f,o)
  end
  reduce(|,partials,init=false)
end

function Base.all(f::Function,a::GenericPArray)
  partials = map(own_values(a)) do o
    all(f,o)
  end
  reduce(&,partials,init=true)
end

Base.maximum(a::GenericPArray) = maximum(identity,a)
function Base.maximum(f::Function,a::GenericPArray)
  partials = map(own_values(a)) do o
    maximum(f,o,init=typemin(eltype(a)))
  end
  reduce(max,partials,init=typemin(eltype(a)))
end

Base.minimum(a::GenericPArray) = minimum(identity,a)
function Base.minimum(f::Function,a::GenericPArray)
  partials = map(own_values(a)) do o
    minimum(f,o,init=typemax(eltype(a)))
  end
  reduce(min,partials,init=typemax(eltype(a)))
end

function Base.findmax(f::Function,a::GenericPArray)
  init = typemin(eltype(a))
  pairs = map(own_values(a),partition(axes(a,1))) do o,ra
    _findmax_pairs(f,o,ra,init=init)
  end
  Tuple(reduce(max,pairs,init=(init=>0)))
end

function Base.findmin(f::Function,a::GenericPArray)
  init = typemax(eltype(a))
  pairs = map(own_values(a),partition(axes(a,1))) do o,ra
    _findmin_pairs(f,o,ra,init=init)
  end
  Tuple(reduce(min,pairs,init=(init=>0)))
end

function Base.collect(v::GenericPArray)
  own_values_v = own_values(v)
  own_to_global_v = map(own_to_global,partition(axes(v,1)))
  vals = gather(own_values_v,destination=:all)
  ids = gather(own_to_global_v,destination=:all)
  n = length(v)
  T = eltype(v)
  map(vals,ids) do val,id
    u = Vector{T}(undef,n)
    for (a,b) in zip(val,id)
      u[b] = a
    end
    u
  end |> getany
end

function Base.:*(a::Number,b::GenericPArray)
  values = map(partition(b)) do values
    a*values
  end
  GenericPArray(values,partition(axes(b,1)))
end

function Base.:*(b::GenericPArray,a::Number)
  a*b
end

function Base.:/(b::GenericPArray,a::Number)
  (1/a)*b
end

for op in (:+,:-)
  @eval begin
    function Base.$op(a::GenericPArray)
      values = map($op,partition(a))
      GenericPArray(values,partition(axes(a,1)))
    end
    function Base.$op(a::GenericPArray,b::GenericPArray)
      @check size(a) == size(b)
      values = map($op,partition(a),partition(b))
      GenericPArray(values,partition(axes(a,1)),a.unpartitioned_axes)
    end
  end
end

function Base.reduce(op,a::GenericPArray;neutral=PartitionedArrays.neutral_element(op,eltype(a)),kwargs...)
  b = map(own_values(a)) do a
    reduce(op,a,init=neutral)
  end
  reduce(op,b;kwargs...)
end

function Base.sum(a::GenericPArray)
  reduce(+,a,init=zero(eltype(a)))
end

function LinearAlgebra.dot(a::GenericPArray,b::GenericPArray)
  c = map(dot,own_values(a),own_values(b))
  sum(c)
end

function LinearAlgebra.rmul!(a::GenericPArray,v::Number)
  map(partition(a)) do l
    rmul!(l,v)
  end
  a
end

function LinearAlgebra.norm(a::GenericPArray,p::Real=2)
  contibs = map(own_values(a)) do oid_to_value
    norm(oid_to_value,p)^p
  end
  reduce(+,contibs;init=zero(eltype(contibs)))^(1/p)
end

function Base.:*(a::PSparseMatrix,b::GenericPVector)
  Ta = eltype(a)
  Tb = eltype(b)
  T = typeof(zero(Ta)*zero(Tb)+zero(Ta)*zero(Tb))
  c = GenericPArray{Vector{T}}(undef,partition(axes(a,1)))
  mul!(c,a,b)
  c
end

function Base.:*(a::PSparseMatrix,b::GenericPMatrix)
  Ta = eltype(a)
  Tb = eltype(b)
  T = typeof(zero(Ta)*zero(Tb)+zero(Ta)*zero(Tb))
  c = GenericPArray{Matrix{T}}(undef,partition(axes(a,1)),axes(b,2))
  mul!(c,a,b)
  c
end

function Base.:*(a::GenericPMatrix,b::AbstractVector)
  Ta = eltype(a)
  Tb = eltype(b)
  T = typeof(zero(Ta)*zero(Tb)+zero(Ta)*zero(Tb))
  c = GenericPArray{Vector{T}}(undef,partition(axes(a,1)))
  mul!(c,a,b,one(T),zero(T))
end

function Base.:*(a::GenericPMatrix,b::AbstractMatrix)
  Ta = eltype(a)
  Tb = eltype(b)
  T = typeof(zero(Ta)*zero(Tb)+zero(Ta)*zero(Tb))
  c = GenericPArray{Matrix{T}}(undef,partition(axes(a,1)),axes(b,2))
  mul!(c,a,b,one(T),zero(T))
end

function Base.:*(a::Adjoint{T,<:GenericPMatrix} where T,b::GenericPVector)
  Ta = eltype(a)
  Tb = eltype(b)
  T = typeof(zero(Ta)*zero(Tb)+zero(Ta)*zero(Tb))
  c = Vector{T}(undef,(size(a,1),))
  mul!(c,a,b,one(T),zero(T))
end

function Base.:*(a::Adjoint{T,<:GenericPMatrix} where T,b::GenericPMatrix)
  Ta = eltype(a)
  Tb = eltype(b)
  T = typeof(zero(Ta)*zero(Tb)+zero(Ta)*zero(Tb))
  c = Matrix{T}(undef,(size(a,1),size(b,2)))
  mul!(c,a,b,one(T),zero(T))
end

function Base.:*(a::Adjoint{T,<:GenericPMatrix} where T,b::PVector{<:AbstractParamVector})
  Ta = eltype(a)
  Tb = eltype2(b)
  T = typeof(zero(Ta)*zero(Tb)+zero(Ta)*zero(Tb))
  c = Vector{T}(undef,(size(a,1),))
  pc = parameterise(c,param_length(b))
  mul!(pc,a,b,one(T),zero(T))
end

function LinearAlgebra.mul!(
  c::GenericPArray,
  a::PSparseMatrix,
  b::GenericPArray,
  α::Number,
  β::Number
  )

  @boundscheck @assert PartitionedArrays.matching_own_indices(axes(c,1),axes(a,1))
  @boundscheck @assert PartitionedArrays.matching_own_indices(axes(a,2),axes(b,1))
  if !PartitionedArrays.matching_ghost_indices(axes(a,2),axes(b,1))
    b = _change_layout(b,partition(axes(a,2)))
  end
  # Start the exchange
  t = consistent!(b)
  # Meanwhile, process the owned blocks
  map(own_values(c),own_values(a),own_values(b)) do co,aoo,bo
    if β != 1
      β != 0 ? rmul!(co,β) : fill!(co,zero(eltype(co)))
    end
    mul!(co,aoo,bo,α,1)
  end
  # Wait for the exchange to finish
  wait(t)
  # process the ghost block
  map(own_values(c),own_ghost_values(a),ghost_values(b)) do co,aoh,bh
    mul!(co,aoh,bh,α,1)
  end
  c
end

function LinearAlgebra.mul!(
  c::GenericPVector,
  a::GenericPMatrix,
  b::GenericPVector,
  α::Number,
  β::Number
  )

  t = consistent!(b)
  wait(t)
  map(own_values(c),own_values(a),partition(b)) do co,ao,lb
    if β != 1
      β != 0 ? rmul!(co,β) : fill!(co,zero(eltype(co)))
    end
    mul!(co,ao,lb,α,1)
  end
  c
end

function LinearAlgebra.mul!(
  c::GenericPMatrix,
  a::GenericPMatrix,
  b::GenericPMatrix,
  α::Number,
  β::Number
  )

  t = consistent!(b)
  wait(t)
  map(own_values(c),own_values(a),partition(b)) do co,ao,lb
    if β != 1
      β != 0 ? rmul!(co,β) : fill!(co,zero(eltype(co)))
    end
    mul!(co,ao,lb,α,1)
  end
  c
end

function LinearAlgebra.mul!(
  c::GenericPVector,
  a::GenericPMatrix,
  b::AbstractVector{<:Number},
  α::Number,
  β::Number
  )

  map(own_values(c),own_values(a)) do co,ao
    mul!(co,ao,b,α,β)
  end
  c
end

function LinearAlgebra.mul!(
  c::PVector,
  a::GenericPMatrix,
  b::AbstractParamVector,
  α::Number,
  β::Number
  )

  map(own_values(c),own_values(a)) do co,ao
    mul!(co,ao,b,α,β)
  end
  consistent!(c) |> wait
  c
end

function LinearAlgebra.mul!(
  c::AbstractParamArray,
  a::GenericPMatrix,
  b::AbstractParamArray,
  α::Number,
  β::Number
  )

  cd = get_all_data(c)
  bd = get_all_data(b)
  if β != 1
    β != 0 ? rmul!(cd,β) : fill!(cd,zero(eltype(cd)))
  end
  map(own_values(a),partition(axes(a,1))) do ao,rows
    o2g = own_to_global(rows)
    @views cd[o2g,:] .+= α .* (ao*bd)
  end
  c
end

function LinearAlgebra.mul!(
  c::GenericPMatrix,
  a::GenericPMatrix,
  b::AbstractMatrix{<:Number},
  α::Number,
  β::Number
  )

  map(own_values(c),own_values(a)) do co,ao
    mul!(co,ao,b,α,β)
  end
  c
end

function LinearAlgebra.mul!(
  c::AbstractVector{<:Number},
  at::Adjoint{<:Any,<:GenericPMatrix},
  b::GenericPVector,
  α::Number,β::Number
  )

  a = at.parent
  G = map(own_values(a),own_values(b)) do ao,bo
    ao'*bo
  end
  r = _gather_reduce(+,G)
  if β == 0
    c .= α.*r
  else
    rmul!(c,β)
    c .+= α.*r
  end
  c
end

function LinearAlgebra.mul!(
  c::AbstractMatrix{<:Number},
  at::Adjoint{<:Any,<:GenericPMatrix},
  b::GenericPMatrix,
  α::Number,β::Number
  )

  a = at.parent
  G = map(own_values(a),own_values(b)) do ao,bo
    ao'*bo
  end
  r = _gather_reduce(+,G)
  if β == 0
    c .= α.*r
  else
    rmul!(c,β)
    c .+= α.*r
  end
  c
end

function LinearAlgebra.mul!(
  c::AbstractParamArray,
  at::Adjoint{<:Any,<:GenericPMatrix},
  b::PVector{<:AbstractParamVector},
  α::Number,β::Number
  )
  
  a = at.parent
  G = map(own_values(a),own_values(b)) do ao,bo
    ao'*get_all_data(bo)
  end
  r = _gather_reduce(+,G)
  if β == 0
    copyto!(get_all_data(c),rmul!(r,α))
  else
    rmul!(get_all_data(c),β)
    axpy!(α,r,get_all_data(c))
  end
  c
end

function LinearAlgebra.axpy!(α,a::GenericPArray,b::GenericPArray)
  @check partition(axes(a,1)) === partition(axes(b,1))
  map(partition(a),partition(b)) do a,b
    LinearAlgebra.axpy!(α,a,b)
  end
  consistent!(b) |> wait
  return b
end

function Base.hcat(A::GenericPMatrix,B::GenericPMatrix)
  @boundscheck @assert PartitionedArrays.matching_own_indices(axes(A,1),axes(B,1))
  @check size(A,2) == size(B,2)
  values = map(partition(A),partition(B)) do la,lb
    hcat(la,lb)
  end
  GenericPArray(values,partition(axes(A,1)),(size(A,2)+size(B,2),))
end

# necessary 

function PartitionedArrays.own_values(a::AbstractArray{<:Any,N},i) where N
  view(a,own_to_local(i),_ncolons(Val{N-1}())...)
end

function PartitionedArrays.ghost_values(a::AbstractArray{<:Any,N},i) where N
  view(a,ghost_to_local(i),_ncolons(Val{N-1}())...)
end

function PartitionedArrays.own_values(a::ConsecutiveParamArray,indices)
  ConsecutiveParamArray(own_values(a.data,indices))
end

function PartitionedArrays.ghost_values(a::ConsecutiveParamArray,indices)
  ConsecutiveParamArray(ghost_values(a.data,indices))
end

# index handling 

flat_row_partition(a::GenericPArray) = flat_row_partition(a.index_partition)
row_partition(a::GenericPArray) = row_partition(a.index_partition)
col_partition(a::GenericPArray) = col_partition(a.index_partition)

# utils 

function _change_layout(b::GenericPArray,new_idx_partition)
  N = ndims(b)
  usizes = map(length,b.unpartitioned_axes) 
  new_parts = map(own_values(b),new_idx_partition) do bo,ra
    nl = local_length(ra)
    new_lb = similar(bo,(nl,usizes...))
    @views begin
      new_lb[own_to_local(ra),_ncolons(Val{N-1}())...] .= bo
    end
    new_lb
  end
  GenericPArray(new_parts,new_idx_partition,b.unpartitioned_axes)
end

second(p::Pair) = p.second

function _findmin_pairs(f,v,ra;init=typemax(eltype(v)))
  local min_owned
  min_val = init
  for (i,val) in enumerate(v)
    fv = f(val)
    if fv < min_val
      min_val = fv
      min_owned = i
    end
  end
  gi = own_to_global(ra)[min_owned]
  return min_val => gi
end

function _findmax_pairs(f,v,ra;init=typemin(eltype(v)))
  local max_owned
  max_val = init
  for (i,val) in enumerate(v)
    fv = f(val)
    if fv > max_val
      max_val = fv
      max_owned = i
    end
  end
  gi = own_to_global(ra)[max_owned]
  return max_val => gi
end