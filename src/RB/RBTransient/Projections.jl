#TODO Functionalities are not yet implemented for a general high order PDE; eventually,
# it would be desirable to have something like:
# abstract type HighDimProjection <: Projection end
# abstract type TransientProjection <: HighDimProjection end

"""
    abstract type TransientProjection <: Projection end

Abstract type for projection operators in transient reduced-basis problems.
A `TransientProjection` wraps a spatial projection and a temporal projection
and provides methods to project and reconstruct space–time snapshot vectors.

# Interface
- `get_projection_space(a)`: return the spatial `Projection`.
- `get_projection_time(a)`: return the temporal `Projection`.
- `get_basis_space(a)` / `get_basis_time(a)`: retrieve the spatial / temporal
  basis matrices.
"""
abstract type TransientProjection <: Projection end

get_projection_space(a::Projection) = a
get_projection_time(a::Projection) = @notimplemented
get_projection_space(a::TransientProjection) = @abstractmethod
get_projection_time(a::TransientProjection) = @abstractmethod

get_basis_space(a::Projection) = get_basis(get_projection_space(a))
get_basis_time(a::Projection) = get_basis(get_projection_time(a))
ParamDataStructures.num_space_dofs(a::Projection) = num_fe_dofs(get_projection_space(a))
ParamDataStructures.num_times(a::Projection) = num_fe_dofs(get_projection_time(a))
RBSteady.num_fe_dofs(a::TransientProjection) = num_space_dofs(a)*num_times(a)

function RBSteady.project!(
  x̂::ConsecutiveParamVector,
  a::TransientProjection,
  x::ConsecutiveParamVector
  )

  nt = num_times(a)
  @check Int(param_length(x) / param_length(x̂)) == nt
  np = param_length(x̂)
  @inbounds for ip in eachindex(x̂)
    ipt = ip:np:np*nt
    xpt = vec(view(x.data,:,ipt))
    x̂p = view(x̂.data,:,ip)
    project!(x̂p,a,xpt)
  end
end

function RBSteady.inv_project!(
  x::ConsecutiveParamVector,
  a::TransientProjection,
  x̂::ConsecutiveParamVector
  )

  nt = num_times(a)
  @check Int(param_length(x) / param_length(x̂)) == nt
  np = param_length(x̂)
  @inbounds for ip in eachindex(x̂)
    ipt = ip:np:np*nt
    xpt = vec(view(x.data,:,ipt))
    x̂p = x̂[ip]
    inv_project!(xpt,a,x̂p)
  end
end

function Algebra.allocate_in_domain(
  a::TransientProjection,
  x::V
  ) where V<:AbstractParamVector

  x̂ = allocate_vector(eltype(V),num_reduced_dofs(a))
  nt = num_times(a)
  np = Int(param_length(x) / nt)
  return parameterise(x̂,np)
end

function Algebra.allocate_in_range(
  a::TransientProjection,
  x̂::V
  ) where V<:AbstractParamVector

  x = allocate_vector(eltype(V),num_space_dofs(a))
  nt = num_times(a)
  npt = param_length(x̂) * nt
  return parameterise(x,npt)
end

"""
    struct KroneckerProjection <: TransientProjection
      projection_space::Projection
      projection_time::Projection
    end

Projection operator for transient problems, containing a spatial projection and
a temporal one. The space-time projection operator is equal to

`projection_time ⊗ projection_space`

which, for efficiency reasons, is never explicitly computed
"""
struct KroneckerProjection <: TransientProjection
  projection_space::Projection
  projection_time::Projection
end

function kron_projection(red::KroneckerReduction,s::TransientSnapshots,args...)
  basis_space,basis_time = tucker(red.reductions,s,args...)
  projection_space = PODProjection(basis_space)
  projection_time = PODProjection(basis_time)
  return projection_space,projection_time
end

function kron_projection(red::KroneckerReduction,s::TransientSparseSnapshots,args...)
  basis_space,basis_time = tucker(red.reductions,s,args...)
  basis_space′ = recast(basis_space,s)
  projection_space = PODProjection(basis_space′)
  projection_time = PODProjection(basis_time)
  return projection_space,projection_time
end

function RBSteady.projection(red::KroneckerReduction,s::TransientSnapshots)
  ps,pt = kron_projection(red,s)
  return KroneckerProjection(ps,pt)
end

function RBSteady.projection(
  red::KroneckerReduction,
  s::TransientSnapshots,
  X::MatrixOrTensor
  )

  ps,pt = kron_projection(red,s,X)
  psX = NormedProjection(ps,X)
  return KroneckerProjection(psX,pt)
end

get_projection_space(a::KroneckerProjection) = a.projection_space
get_projection_time(a::KroneckerProjection) = a.projection_time

RBSteady.get_basis(a::KroneckerProjection) = kron(get_basis_time(a),get_basis_space(a))
RBSteady.num_reduced_dofs(a::KroneckerProjection) = num_reduced_dofs(a.projection_space)*num_reduced_dofs(a.projection_time)
RBSteady.get_norm_matrix(a::KroneckerProjection) = get_norm_matrix(a.projection_space)

function RBSteady.project!(
  x̂::AbstractVector{<:Number},
  a::KroneckerProjection,
  x::AbstractVector{<:Number}
  )

  ns = num_reduced_dofs(a.projection_space)
  nt = num_reduced_dofs(a.projection_time)
  X̂ = reshape(x̂,ns,nt)

  Ns = num_fe_dofs(a.projection_space)
  Nt = num_fe_dofs(a.projection_time)
  X = reshape(x,Ns,Nt)

  basis_time = get_basis(a.projection_time)

  project!(X̂,a.projection_space,X*basis_time)
end

function RBSteady.inv_project!(
  x::AbstractVector{<:Number},
  a::KroneckerProjection,
  x̂::AbstractVector{<:Number}
  )

  Ns = num_fe_dofs(a.projection_space)
  Nt = num_fe_dofs(a.projection_time)
  X = reshape(x,Ns,Nt)

  ns = num_reduced_dofs(a.projection_space)
  nt = num_reduced_dofs(a.projection_time)
  X̂ = reshape(x̂,ns,nt)

  basis_time = get_basis(a.projection_time)

  inv_project!(X,a.projection_space,X̂*basis_time')
end

function RBSteady.galerkin_projection(
  proj_left::KroneckerProjection,
  a::KroneckerProjection,
  args...
  )

  proj_basis_space = galerkin_projection(get_basis_space(proj_left),get_basis_space(a))
  proj_basis_time = galerkin_projection(get_basis_time(proj_left),get_basis_time(a))
  proj_basis = kron(proj_basis_time,proj_basis_space)
  return ReducedProjection(proj_basis)
end

function RBSteady.galerkin_projection(
  proj_left::KroneckerProjection,
  a::KroneckerProjection,
  proj_right::KroneckerProjection,
  combine
  )

  proj_basis_space = galerkin_projection(
    get_basis_space(proj_left),
    get_basis_space(a),
    get_basis_space(proj_right))

  proj_basis_time = galerkin_projection(
    get_basis_time(proj_left),
    get_basis_time(a),
    get_basis_time(proj_right),
    combine)

  nleft = num_reduced_dofs(proj_left)
  ns = num_reduced_dofs(a.projection_space)
  nt = num_reduced_dofs(a.projection_time)
  n = num_reduced_dofs(a)
  nright = num_reduced_dofs(proj_right)

  T = projection_eltype(proj_left)
  S = projection_eltype(a)
  TS = promote_type(T,S)
  proj_basis = zeros(TS,nleft,n,nright)
  @inbounds for it = 1:nt, is = 1:ns
    ist = (it-1)*ns+is
    @views proj_basis[:,ist,:] = kron(proj_basis_time[:,it,:],proj_basis_space[:,is,:])
  end

  return ReducedProjection(proj_basis)
end

function RBSteady.galerkin_projection(
  proj_left::KroneckerProjection,
  a::GalerkinProjectable,
  args...
  )

  nt = num_times(proj_left)
  np = Int(param_length(a.array) / nt)
  proj_a_space = galerkin_projection(get_basis_space(proj_left),get_basis(a))
  proj_a_spacetime = galerkin_projection(get_basis_time(proj_left),change_mode(proj_a_space,np))
  proj_a_time_space = permutedims(reshape(proj_a_spacetime,size(proj_a_spacetime,1),:,np),(2,1,3))
  proj = reshape(proj_a_time_space,:,np)
  return ReducedProjection(proj)
end

function RBSteady.galerkin_projection(
  proj_left::KroneckerProjection,
  a::GalerkinProjectable,
  proj_right::KroneckerProjection,
  combine
  )

  nt = num_times(proj_left)
  np = Int(param_length(a.array) / nt)
  ns_left = num_reduced_dofs(proj_left.projection_space)
  ns_right = num_reduced_dofs(proj_right.projection_space)
  nt_left = num_reduced_dofs(proj_left.projection_time)
  nt_right = num_reduced_dofs(proj_right.projection_time)

  proj_a_space = galerkin_projection(
    get_basis_space(proj_left),
    get_basis(a),
    get_basis_space(proj_right))

  a2 = permutedims(proj_a_space,(2,1,3)) # Nμ*Nt x ns_left x ns_right
  a3 = reshape(a2,np,nt,ns_left,ns_right) # Nμ x Nt x ns_left x ns_right
  a4 = reshape(permutedims(a3,(2,3,1,4)),nt,:) # Nt x ns_left*Nμ*ns_right
  proj_a_spacetime = galerkin_projection(
    get_basis_time(proj_left),
    a4,
    get_basis_time(proj_right),
    combine) # nt_left x ns_left*Nμ*ns_right x nt_right

  a5 = reshape(proj_a_spacetime,nt_left,ns_left,np,ns_right,nt_right) # nt_left x ns_left x Nμ x ns_right x nt_right
  a6 = permutedims(a5,(2,1,3,4,5)) # ns_left x nt_left x Nμ x ns_right x nt_right
  proj = reshape(a6,ns_left*nt_left,np,ns_right*nt_right)
  return ReducedProjection(proj)
end

function RBSteady.projection_eltype(a::KroneckerProjection)
  T = projection_eltype(a.projection_space)
  S = projection_eltype(a.projection_time)
  promote_type(T,S)
end

for f in (:DEIM,:SOPT)
  @eval begin
    function RBSteady.$f(a::KroneckerProjection)
      indices_space,interp_space = RBSteady.$f(get_basis_space(a))
      indices_time,interp_time = RBSteady.$f(get_basis_time(a))
      interp = kron(interp_time,interp_space)
      return (indices_space,indices_time),interp
    end
  end
end

# tt interface

struct SequentialProjection{A} <: TransientProjection
  projection::A
end

function RBSteady.projection(red::SequentialReduction,s::TransientSnapshots)
  proj = projection(get_reduction(red),s)
  SequentialProjection(proj)
end

function RBSteady.projection(
  red::SequentialReduction,
  s::TransientSnapshots,
  X::MatrixOrTensor
  )

  proj = projection(get_reduction(red),s,X)
  SequentialProjection(proj)
end

#TODO when new projection operators are implemented, this will have to change
RBSteady.get_cores(a::SequentialProjection) = get_cores(a.projection)
get_cores_space(a::SequentialProjection) = get_cores(a)[1:end-1]
get_core_time(a::SequentialProjection) = get_cores(a)[end]
get_basis_space(a::SequentialProjection) = cores2basis(get_cores_space(a)...)
get_basis_time(a::SequentialProjection) = @notimplemented

ParamDataStructures.num_space_dofs(a::SequentialProjection) = prod(map(c -> size(c,2),get_cores_space(a)))
ParamDataStructures.num_times(a::SequentialProjection) = size(get_core_time(a),2)

DofMaps.get_dof_map(a::SequentialProjection) = get_dof_map(a.projection)

RBSteady.get_basis(a::SequentialProjection) = get_basis(a.projection)
RBSteady.num_reduced_dofs(a::SequentialProjection) = num_reduced_dofs(a.projection)
RBSteady.get_norm_matrix(a::SequentialProjection) = get_norm_matrix(a.projection)
RBSteady.DEIM(a::SequentialProjection) = DEIM(a.projection)
RBSteady.SOPT(a::SequentialProjection) = SOPT(a.projection)

function RBSteady.union_bases(a::SequentialProjection,b::AbstractArray,args...)
  projection′ = union_bases(a.projection,b,args...)
  SequentialProjection(projection′)
end

function RBSteady.galerkin_projection(
  proj_left::SequentialProjection,
  a::SequentialProjection,
  args...
  )

  galerkin_projection(proj_left.projection,a.projection)
end

function RBSteady.galerkin_projection(
  proj_left::SequentialProjection,
  a::SequentialProjection,
  proj_right::SequentialProjection,
  combine
  )

  RBSteady._galerkin_projection(get_dof_map(a),proj_left,a,proj_right,combine)
end

function RBSteady.galerkin_projection(
  proj_left::SequentialProjection,
  a::GalerkinProjectable,
  args...
  )

  nt = num_times(proj_left)
  np = Int(param_length(a.array) / nt)
  proj_a_space = galerkin_projection(get_basis_space(proj_left),get_basis(a))
  proj_core_space = permutedims(reshape(proj_a_space,:,np,nt),(1,3,2)) # ns_left x Nt x Nμ
  pl_time = get_core_time(proj_left) # ns_left x Nt x nt_left
  proj = _contraction(pl_time,proj_core_space) # nt_left x Nμ
  return ReducedProjection(proj)
end

function RBSteady.galerkin_projection(
  proj_left::SequentialProjection,
  a::GalerkinProjectable,
  proj_right::SequentialProjection,
  combine
  )

  nt = num_times(proj_left)
  np = Int(param_length(a.array) / nt)
  ns_left = num_reduced_dofs_space(proj_left)
  ns_right = num_reduced_dofs_space(proj_right)

  proj_a_space = galerkin_projection(
    get_basis_space(proj_left),
    get_basis(a),
    get_basis_space(proj_right))

  a2 = permutedims(proj_a_space,(2,1,3)) # Nμ*Nt x ns_left x ns_right
  a3 = reshape(a2,np,nt,ns_left,ns_right) # Nμ x Nt x ns_left x ns_right
  a4 = permutedims(a3,(3,2,1,4)) # ns_left x Nt x Nμ x ns_right

  pl_time = get_core_time(proj_left) # ns_left x Nt x nt_left
  pr_time = get_core_time(proj_right) # ns_right x Nt x nt_right
  proj_cores = _contraction(pl_time,a4,pr_time,combine) # nt_left x Nμ x nt_right
  return ReducedProjection(proj_cores)
end

function RBSteady.projection_eltype(a::SequentialProjection)
  projection_eltype(a.projection)
end

function RBSteady.project!(
  x̂::AbstractVector{<:Number},
  a::SequentialProjection,
  x::AbstractVector{<:Number}
  )

  project!(x̂,a.projection,x)
end

function RBSteady.inv_project!(
  x::AbstractVector{<:Number},
  a::SequentialProjection,
  x̂::AbstractVector{<:Number}
  )

  inv_project!(x,a.projection,x̂)
end

# multfield interface

function RBSteady.enrich!(
  red::SupremizerReduction{A,B,<:HighDimReduction},
  a::BlockProjection,
  norm_matrix::BlockRankTensor,
  supr_matrix::BlockRankTensor;
  kwargs...
  ) where {A,B}

  red′ = SupremizerReduction(red.reduction.reduction,red.coupling,red.supr_tol)
  enrich!(red′,a,norm_matrix,supr_matrix;kwargs...)
end

function RBSteady.enrich!(
  red::SupremizerReduction{A,B,<:KroneckerReduction},
  a::BlockProjection,
  norm_matrix::BlockMatrix,
  supr_matrix::BlockMatrix
  ) where {A,B}

  tol = RBSteady.get_supr_tol(red)
  a_primal,a_dual... = a.array
  a_primal_space = a_primal.projection_space
  a_primal_time = a_primal.projection_time
  X_primal = norm_matrix[Block(1,1)]
  H_primal = symcholesky(X_primal)
  for i = eachindex(a_dual)
    dual_i_space = get_basis_space(a_dual[i])
    C_primal_dual_i = supr_matrix[Block(1,i+1)]
    supr_space_i = supremizers(H_primal,C_primal_dual_i,dual_i_space)
    a_primal_space = union_bases(a_primal_space,supr_space_i,H_primal)

    dual_i_time = get_basis_time(a_dual[i])
    a_primal_time = time_enrichment(a_primal_time,dual_i_time;tol)
  end
  a[1] = KroneckerProjection(a_primal_space,a_primal_time)
  return
end

"""
    time_enrichment(red::SupremizerReduction,a_primal::Projection,basis_dual;kwargs...) -> AbstractMatrix

Temporal supremizer enrichment. (Approximate) Procedure:

1. for every `b_dual ∈ Col(basis_dual)`
2. compute `Φ_primal_dual = get_basis(a_primal)'*get_basis(b_dual)`
3. compute `v = kernel(Φ_primal_dual)`
4. compute `v′ = orth_complement(v,a_primal)`
5. enrich `a_primal = [a_primal,v′]`
"""
function time_enrichment(a_primal::Projection,basis_dual;kwargs...)
  basis_primal′ = time_enrichment(get_basis(a_primal),basis_dual;kwargs...)
  PODProjection(basis_primal′)
end

function time_enrichment(basis_primal,basis_dual;tol=1e-2)
  basis_pd = basis_primal'*basis_dual
  T = eltype(basis_pd)

  i = 1
  while i ≤ size(basis_pd,2)
    basis_pd_start = view(basis_pd,:,1:i-1)
    basis_pd_i = view(basis_pd,:,i)
    proj = i == 1 ? zeros(T,size(basis_pd,1)) : orth_projection(basis_pd_i,basis_pd_start)
    dist = norm(basis_pd_i-proj)
    if dist ≤ tol
      basis_primal,basis_pd = tenrich(basis_primal,basis_pd,basis_dual,i)
      i = 0
    else
      basis_pd_i .-= proj
    end
    i += 1
  end

  return basis_primal
end

function tenrich(basis_primal,basis_pd,basis_dual,i)
  vi = copy(view(basis_dual,:,i))
  orth_complement!(vi,basis_primal)
  vi ./= norm(vi)
  hcat(basis_primal,vi),vcat(basis_pd,vi'*basis_dual)
end

# utils

function RBSteady._galerkin_projection(
  ::AbstractDofMap,
  proj_left::SequentialProjection,
  a::SequentialProjection,
  proj_right::SequentialProjection,
  combine
  )

  # space
  pl_space = get_cores_space(proj_left)
  a_space = get_cores_space(a)
  pr_space = get_cores_space(proj_right)
  p_space = unbalanced_contractions(pl_space,a_space,pr_space)

  # time
  pl_time = get_core_time(proj_left)
  a_time = get_core_time(a)
  pr_time = get_core_time(proj_right)
  p_time = contraction(pl_time,a_time,pr_time,combine)

  p = sequential_product(p_space...,p_time)
  proj_cores = dropdims(p;dims=(1,2,3))

  return ReducedProjection(proj_cores)
end

function RBSteady._galerkin_projection(
  ::TrivialDofMap,
  proj_left::SequentialProjection,
  a::SequentialProjection,
  proj_right::SequentialProjection,
  combine
  )

  get_core_space(a) = RBSteady.basis2core(get_basis_space(a))

  # space
  pl_space = get_core_space(proj_left)
  a_space = first(get_cores(a))
  pr_space = get_core_space(proj_right)
  p_space = contraction(pl_space,a_space,pr_space)

  # time
  pl_time = get_core_time(proj_left)
  a_time = get_core_time(a)
  pr_time = get_core_time(proj_right)
  p_time = contraction(pl_time,a_time,pr_time,combine)

  p = sequential_product(p_space,p_time)
  proj_cores = dropdims(p;dims=(1,2,3))

  return ReducedProjection(proj_cores)
end

# space-only projections

num_fe_dofs_space(a::Projection) = size(get_basis_space(a),1)
num_reduced_dofs_space(a::Projection) = size(get_basis_space(a),2)

function space_project(a::Projection,x::AbstractArray,args...)
  x̂ = allocate_in_space_domain(a,x)
  space_project!(x̂,a,x,args...)
  return x̂
end

function inv_space_project(a::Projection,x̂::AbstractArray)
  x = allocate_in_space_range(a,x̂)
  inv_space_project!(x,a,x̂)
  return x
end

function space_project!(x̂::AbstractArray,a::Projection,x::AbstractArray)
  basis = get_basis_space(a)
  mul!(x̂,basis',x)
end

function inv_space_project!(x::AbstractArray,a::Projection,x̂::AbstractArray)
  basis = get_basis_space(a)
  mul!(x,basis,x̂)
end

function allocate_in_space_domain(a::Projection,x::V) where V<:AbstractVector
  x̂ = allocate_vector(V,num_reduced_dofs_space(a))
  return x̂
end

function allocate_in_space_range(a::Projection,x̂::V) where V<:AbstractVector
  x = allocate_vector(V,num_fe_dofs_space(a))
  return x
end

function allocate_in_space_domain(a::Projection,X::M) where M<:AbstractMatrix
  X̂ = zeros(eltype(M),num_reduced_dofs_space(a),size(X,2))
  return X̂
end

function allocate_in_space_range(a::Projection,X̂::M) where M<:AbstractMatrix
  X = Matrix{eltype(M)}(undef,num_fe_dofs_space(a),size(X̂,2))
  return X
end

function allocate_in_space_domain(a::Projection,x::V) where V<:AbstractParamVector
  x̂ = allocate_vector(eltype(V),num_reduced_dofs_space(a))
  return parameterise(x̂,param_length(x))
end

function allocate_in_space_range(a::Projection,x̂::V) where V<:AbstractParamVector
  x = allocate_vector(eltype(V),num_fe_dofs_space(a))
  return parameterise(x,param_length(x̂))
end

function num_fe_dofs_space(a::BlockProjection)
  dofs = 0
  for i in eachindex(a)
    dofs += num_fe_dofs_space(a[i])
  end
  return dofs
end

function num_reduced_dofs_space(a::BlockProjection)
  dofs = 0
  for i in eachindex(a)
    dofs += num_reduced_dofs_space(a[i])
  end
  return dofs
end

function to_fe_blocks_space(x::Union{BlockVector,BlockParamVector},a::BlockProjection,args...)
  x
end

function to_fe_blocks_space(x,a::BlockProjection,args...)
  ids = map(num_fe_dofs_space,a.array)
  pushfirst!(ids,1)
  RBSteady.to_blocks(x,cumsum(ids),args...)
end

function to_reduced_blocks_space(x::Union{BlockVector,BlockParamVector},a::BlockProjection,args...)
  x
end

function to_reduced_blocks_space(x,a::BlockProjection,args...)
  ids = map(num_reduced_dofs_space,a.array)
  pushfirst!(ids,1)
  RBSteady.to_blocks(x,cumsum(ids),args...)
end

for (f,g) in zip((:allocate_in_space_domain,:allocate_in_space_range),(:to_fe_blocks_space,:to_reduced_blocks_space))
  @eval begin
    function $f(a::BlockProjection)
      mortar(map($f,a.array))
    end

    function $f(a::BlockProjection,x::BlockVector)
      @check length(a) == blocklength(x)
      mortar(map(i -> $f(a[Block(i)],x[Block(i)]),eachindex(a)))
    end
  end
end

for (f,g) in zip((:space_project!,:inv_space_project!),(:to_fe_blocks_space,:to_reduced_blocks_space))
  ginv = g == :to_fe_blocks_space ? :to_reduced_blocks_space : :to_fe_blocks_space
  @eval begin
    function $f(
      y::Union{BlockArray,BlockParamArray},
      a::BlockProjection,
      x::Union{BlockArray,BlockParamArray}
      )

      for i in eachindex(a)
        yi = blocks(y)[i]
        $f(yi,a[i],x[Block(i)])
      end
    end

    function $f(
      y::Union{AbstractArray,AbstractParamArray},
      a::BlockProjection,
      x::Union{AbstractArray,AbstractParamArray}
      )

      $f($ginv(y,a),a,$g(x,a))
    end
  end
end

# utils 

RBSteady._proj_type(r::SteadyReduction,args...) = RBSteady._proj_type(r.reduction,args...)
RBSteady._proj_type(::KroneckerReduction,args...) = KroneckerProjection
RBSteady._proj_type(::SequentialReduction,args...) = SequentialProjection