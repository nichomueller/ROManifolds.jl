struct LocalProjection{A<:Projection,N} <: Projection
  projections::Array{A,N}
  k::NTuple{N,KmeansResult}
end

LocalProjection(projections::AbstractVector,k::KmeansResult) = LocalProjection(projections,(k,))

const VecLocalProjection{A} = LocalProjection{A,1}
const MatLocalProjection{A} = LocalProjection{A,2}

function projection(lred::LocalReduction,s::Snapshots)
  red = get_reduction(lred)
  k = compute_clusters(lred,s)
  svec = cluster(s,k)
  proj = map(s -> projection(red,s),svec)
  LocalProjection(proj,k)
end

function projection(lred::LocalReduction,s::Snapshots,X::MatrixOrTensor)
  red = get_reduction(lred)
  k = compute_clusters(lred,s)
  svec = cluster(s,k)
  proj = map(s -> projection(red,s,X),svec)
  LocalProjection(proj,k)
end

function galerkin_projection(a::LocalProjection,b::LocalProjection)
  b̂ = map(galerkin_projection,local_vals(a),local_vals(b))
  LocalProjection(b̂,b.k)
end

function galerkin_projection(
  a::LocalProjection,
  b::LocalProjection,
  c::LocalProjection,
  args...
  )

  b̂ = map(
    (pa,pb,pc) -> galerkin_projection(pa,pb,pc,args...),
    local_vals(a),
    local_vals(b),
    local_vals(c)
  )
  LocalProjection(b̂,b.k)
end

local_vals(a) = @abstractmethod
local_vals(a::LocalProjection) = a.projections

function local_vals(a::BlockProjection)
  litems = map(local_vals,a.array)
  nlitems = length(first(litems))
  map(1:nlitems) do i
    BlockProjection(getindex.(litems,i))
  end
end

function local_vals(a::RBSpace)
  space = get_fe_space(a)
  lsubspace = local_vals(get_reduced_subspace(a))
  map(x -> reduced_subspace(space,x),lsubspace)
end

local_proj_to_proj(a::Projection,b::AbstractVector{<:Projection}) = @abstractmethod
local_proj_to_proj(a::LocalProjection,b::AbstractVector{<:Projection}) = LocalProjection(b,a.k)

get_clusters(a) = @abstractmethod
get_clusters(a::LocalProjection) = a.k
get_clusters(a::BlockProjection) = get_clusters(testitem(a))
get_clusters(a::RBSpace) = get_clusters(get_reduced_subspace(a))

function get_local(a,r::Realisation)
  map(r) do μ
    get_local(a,μ)
  end
end

get_local(a::Projection,μ::AbstractVector) = a

function get_local(a::VecLocalProjection,μ::AbstractVector)
  k, = get_clusters(a)
  lab = get_label(k,μ)
  local_vals(a)[lab]
end

function get_local(a::MatLocalProjection,μ::AbstractVector)
  k,l = get_clusters(a)
  labk = get_label(k,μ)
  labl = get_label(l,μ)
  local_vals(a)[labk,labl]
end

function get_local(a::BlockProjection,μ::AbstractVector)
  BlockProjection(map(p -> get_local(p,μ),a.array))
end

function get_local(a::RBSpace,μ::AbstractVector)
  space = get_fe_space(a)
  lsubspace = get_local(get_reduced_subspace(a),μ)
  reduced_subspace(space,lsubspace)
end

function enrich!(
  red::SupremizerReduction{A,B,<:LocalReduction},
  a::BlockProjection,
  norm_matrix::BlockMatrix,
  supr_matrix::BlockMatrix
  ) where {A,B}

  a_primal,a_dual... = a.array
  X_primal = norm_matrix[Block(1,1)]
  H_primal = symcholesky(X_primal)
  a_primal_loc = local_vals(a_primal)
  for j in eachindex(a_primal_loc)
    pj = a_primal_loc[j]
    for i = eachindex(a_dual)
      a_dual_i_loc = local_vals(a_dual[i])
      dij = get_basis(a_dual_i_loc[j])
      C_primal_dual_i = supr_matrix[Block(1,i+1)]
      supr_i = supremizers(H_primal,C_primal_dual_i,dij)
      pj = union_bases(pj,supr_i,H_primal)
    end
    a_primal_loc[j] = pj
  end
  a[1] = local_proj_to_proj(a_primal,a_primal_loc)
  return
end

function enrich!(
  red::SupremizerReduction{A,B,<:LocalReduction},
  a::BlockProjection,
  norm_matrix::BlockRankTensor,
  supr_matrix::BlockRankTensor
  ) where {A,B}

  a_primal,a_dual... = a.array
  X_primal = norm_matrix[Block(1,1)]
  H_primal = symcholesky(X_primal)
  a_primal_loc = local_vals(a_primal)
  for j in eachindex(a_primal_loc)
    pj = a_primal_loc[j]
    for i = eachindex(a_dual)
      a_dual_i_loc = local_vals(a_dual[i])
      dij = get_cores(a_dual_i_loc[j])
      C_primal_dual_i = supr_matrix[Block(1,i+1)]
      supr_ij = tt_supremizers(H_primal,C_primal_dual_i,dij)
      pj = union_bases(pj,supr_ij,X_primal)
    end
    a_primal_loc[j] = pj
  end
  a[1] = local_proj_to_proj(a_primal,a_primal_loc)
  return
end

for f in (:cluster,:cluster_sort)
  @eval begin
    function $f(a,red::LocalReduction)
      r = _get_realisation(a)
      k = compute_clusters(red,r)
      $f(a,k)
    end

    function $f(a,k::KmeansResult)
      labels = get_label(k,a)
      $f(a,labels)
    end

    function $f(a,labels::AbstractVector)
      cluster_ids = group_ilabels(labels)
      $f(a,cluster_ids)
    end
  end
end

function cluster(a,cluster_ids::Table)
  cluster_cache = array_cache(cluster_ids)
  _ids = getindex!(cluster_cache,cluster_ids,1)
  S = typeof(_cluster(a,_ids))
  cache = Vector{S}(undef,length(cluster_ids))

  for icluster in 1:length(cluster_ids)
    ids = getindex!(cluster_cache,cluster_ids,icluster)
    cache[icluster] = _cluster(a,ids)
  end

  return cache
end

function cluster_sort(a,cluster_ids::Table)
  ids′ = sortperm(cluster_ids.data)
  _cluster(a,ids′)
end

# utils

function compute_clusters(red::LocalReduction,r::AbstractRealisation)
  Random.seed!(1234)
  pmat = _get_params_marix(r)
  k = kmeans(pmat,red.ncentroids)
  return k
end

function compute_clusters(red::LocalReduction,s::AbstractSnapshots)
  compute_clusters(red,get_realisation(s))
end

function get_label(k::KmeansResult,a)
  get_label(k,_get_realisation(a))
end

function get_label(k::KmeansResult,r::Realisation)
  map(r) do μ
    get_label(k,μ)
  end
end

function get_label(k::KmeansResult,x::AbstractVector{<:Number})
  length(k.counts) == 1 && return 1
  dists = centroid_distances(k,x)
  argmin(dists)
end

get_centers(k::KmeansResult) = eachcol(k.centers)

function centroid_distances(k::KmeansResult,x::AbstractVector{<:Number})
  centers = get_centers(k)
  dists = zeros(length(centers))
  for (i,y) in enumerate(centers)
    dists[i] = norm(x-y)
  end
  return dists
end

function centroid_distances(k::KmeansResult,x::AbstractMatrix)
  dists = zeros(size(x,2))
  for i in axes(x,2)
    xi = view(x,:,i)
    dists[i] = minimum(centroid_distances(k,xi))
  end
  return dists
end

function compute_ncentroids(
  r::AbstractRealisation;
  init=min(4,num_params(r)),
  iend=min(16,floor(Int,num_params(r)/2))
  )

  Random.seed!(1234)
  pmat = _get_params_marix(r)
  kvars = zeros(iend-init+1)
  all_ncentroids = init:iend
  for (i,ncentroids) in enumerate(all_ncentroids)
    k = kmeans(pmat,ncentroids)
    kvars[i] = kmeans_variance(k,pmat)
  end
  elbow = _compute_elbow(kvars)
  all_ncentroids[elbow]
end

for f in (:group_labels,:get_group_to_labels,:group_ilabels,:get_group_to_ilabels)
  @eval begin
    function DofMaps.$f(q,k::KmeansResult)
      labels = get_label(k,q)
      DofMaps.$f(labels)
    end
  end
end

function kmeans_variance(k::KmeansResult,pmat::AbstractMatrix)
  errs = 0.0
  for α in eachcol(pmat)
    lab = get_label(k,α)
    β = view(k.centers,:,lab)
    errs += norm(α-β)^2
  end
  return errs
end

function _compute_elbow(v::AbstractVector)
  dv = zeros(length(v)-1)
  for i in 1:length(dv)
    dv[i] = abs(v[i+1]-v[i]) / v[i]
  end
  argmin(dv)
end

_get_realisation(r::AbstractRealisation) = get_params(r)
_get_realisation(s::AbstractSnapshots) = get_realisation(s)

_get_params_marix(r::AbstractRealisation) = stack(ParamDataStructures._get_params(r))

function _cluster(a,i)
  @abstractmethod
end

function _cluster(r::Realisation,inds::AbstractVector)
  r[inds]
end

function _cluster(s::GenericSnapshots,inds::AbstractVector)
  sinds = select_snapshots(s,inds)
  data = collect(get_all_data(sinds))
  GenericSnapshots(data,get_param_data(sinds),get_dof_map(sinds),get_realisation(sinds))
end

function _cluster(s::AbstractBlockSnapshots,inds::AbstractVector)
  array = map(sj -> _cluster(sj,inds),blocks(s))
  pdata = _cluster(get_param_data(s),inds)
  return BlockSnapshots(array,pdata)
end

function _cluster(a::ConsecutiveParamArray{T,N},inds::AbstractVector) where {T,N}
  item = param_getindex(a,first(inds))
  data = zeros(T,size(item)...,length(inds))
  @inbounds @views for (ij,j) in enumerate(inds)
    aj = param_getindex(a,j)
    data[_ncolons(Val{N}())...,ij] .= aj
  end
  ConsecutiveParamArray(data)
end

function _cluster(a::AbstractParamArray,inds::AbstractVector)
  data = [a[i] for i in inds]
  ParamArray(data)
end

function _cluster(a::BlockParamArray,inds::AbstractVector)
  mortar(map(ai -> _cluster(ai,inds),blocks(a)))
end

function _cluster(a::RBParamVector,inds::AbstractVector)
  data = _cluster(a.data,inds)
  fe_data = _cluster(a.fe_data,inds)
  RBParamVector(data,fe_data)
end
